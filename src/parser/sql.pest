WHITESPACE = _{ " " | "\t" | "\n" | "\r" }
COMMENT = _{ ("--" ~ (!NEWLINE ~ ANY)* ~ NEWLINE) | ("/*" ~ (!"*/" ~ ANY)* ~ "*/") }

NULL = @{ "NULL" }
BOOLEAN_LITERAL = @{ "TRUE" | "FALSE" | "true" | "false" }
IDENTIFIER = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }
QUOTED_IDENTIFIER = @{ "`" ~ (!("`" | NEWLINE) ~ ANY)* ~ "`" }
STRING_LITERAL = @{ "'" ~ (!"'" ~ ANY | "\\'")* ~ "'" }
COMMA = _{ "," }
EQUALS = _{ "=" }
NOT_EQUALS = _{ "!=" }
LESS_THAN = _{ "<" }
GREATER_THAN = _{ ">" }
LESS_THAN_EQUALS = _{ "<=" }
GREATER_THAN_EQUALS = _{ ">=" }
LIKE = _{ "LIKE" }
IN = _{ "IN" }
IS_NULL = _{ "IS" ~ "NULL" }
IS_NOT_NULL = _{ "IS" ~ "NOT" ~ "NULL" }
AND = _{ "AND" }
OR = _{ "OR" }
AT_MARK = { "@" }

IF_EXISTS = _{ ^"IF" ~ ^"EXISTS" }
IF_NOT_EXISTS = { ^"IF" ~ ^"NOT" ~ ^"EXISTS" }

CREATE_DATABASE = {
    ^"CREATE" ~ ^"DATABASE" ~ IF_NOT_EXISTS? ~ QUOTED_IDENTIFIER ~ DATABASE_OPTIONS
}

CHARACTER_SET = { ^"CHARACTER" ~ ^"SET" ~ EQUALS? ~ IDENTIFIER }
COLLATE = { ^"COLLATE" ~ EQUALS? ~ IDENTIFIER }
ENCRYPTION = { ^"ENCRYPTION" ~ EQUALS? ~ YES_OR_NO }
YES_OR_NO = { "'Y'" | "'N'" }
DATABASE_OPTION = {
    ^"DEFAULT"? ~ ( CHARACTER_SET | COLLATE | ENCRYPTION )
}
DATABASE_OPTIONS = { DATABASE_OPTION* }

USE_DATABASE = { ^"USE" ~ QUOTED_IDENTIFIER }

CREATE_TABLE = {
    ^"CREATE" ~ ^"TABLE" ~ IF_NOT_EXISTS? ~ QUOTED_IDENTIFIER ~ "(" ~ TABLE_SPECS ~ ")" ~ TABLE_OPTIONS?
}

TABLE_SPECS = { TABLE_SPEC+ }
TABLE_OPTIONS = {
    (TABLE_OPTION ~ ("" ~ TABLE_OPTION)*)?
}

TABLE_OPT_AUTO_INCREMENT = { ^"AUTO_INCREMENT" ~ EQUALS? ~ NUMBER }
TABLE_OPT_CHARSET = { ^"DEFAULT"? ~ ^"CHARSET" ~ EQUALS? ~ IDENTIFIER }
TABLE_OPT_COLLATE = { ^"DEFAULT"? ~ ^"COLLATE" ~ EQUALS? ~ IDENTIFIER }
TABLE_OPT_COMMENT = { ^"COMMENT" ~ EQUALS? ~ STRING_LITERAL }
TABLE_OPT_ENGINE = { ^"ENGINE" ~ EQUALS? ~ IDENTIFIER }
TABLE_OPT_ROW_FORMAT = { ^"ROW_FORMAT" ~ EQUALS? ~ IDENTIFIER }
TABLE_OPT_STATS_PERSISTENT = { ^"STATS_PERSISTENT" ~ EQUALS? ~ ( IDENTIFIER | NUMBER ) }

TABLE_OPTION = {
    TABLE_OPT_AUTO_INCREMENT |
    TABLE_OPT_CHARSET |
    TABLE_OPT_COLLATE |
    TABLE_OPT_COMMENT |
    TABLE_OPT_ENGINE |
    TABLE_OPT_ROW_FORMAT | 
    TABLE_OPT_STATS_PERSISTENT
}

TABLE_SPEC = _{
    COLUMN_DEFINITION |
    PRIMARY_KEY |
    FOREIGN_KEY |
    INDEX_DEFINITION
}

COLUMN_DEFINITION = {
    QUOTED_IDENTIFIER ~ DATA_TYPE ~ COLUMN_CONSTRAINT* ~ COMMA?
}

PRIMARY_KEY = {
    ^"CONSTRAINT" ~ INDEX_NAME ~ ^"PRIMARY" ~ ^"KEY" ~ "(" ~ QUOTED_IDENTIFIER ~ ("," ~ QUOTED_IDENTIFIER)* ~ ")" ~ COMMA? |
    ^"PRIMARY KEY" ~ "(" ~ QUOTED_IDENTIFIER ~ ("," ~ QUOTED_IDENTIFIER)* ~ ")" ~ COMMA?
}

FOREIGN_KEY = {
    ^"CONSTRAINT" ~ INDEX_NAME ~ ^"FOREIGN" ~ ^"KEY" ~ "(" ~ QUOTED_IDENTIFIER ~ ("," ~ QUOTED_IDENTIFIER)* ~ ")" ~ "REFERENCES" ~ TABLE_NAME ~ "(" ~ QUOTED_IDENTIFIER ~ ("," ~ QUOTED_IDENTIFIER)* ~ ")" ~ FK_ON_UPDATE? ~ COMMA? |
    ^"FOREIGN KEY" ~ "(" ~ QUOTED_IDENTIFIER ~ ("," ~ QUOTED_IDENTIFIER)* ~ ")" ~ "REFERENCES" ~ TABLE_NAME ~ "(" ~ QUOTED_IDENTIFIER ~ ("," ~ QUOTED_IDENTIFIER)* ~ ")" ~ FK_ON_UPDATE? ~ COMMA?
}

FK_ON_UPDATE = {
    ^"ON" ~ ^"UPDATE" ~ ^"CASCADE"
}

INDEX_DEFINITION = {
    INDEX_TYPE ~ INDEX_NAME ~ "(" ~ QUOTED_IDENTIFIER ~ ("," ~ QUOTED_IDENTIFIER)* ~ ")" ~ COMMA?
}

INDEX_TYPE = { (^"UNIQUE" ~ ^"KEY" | ^"KEY") }
INDEX_NAME = { QUOTED_IDENTIFIER }
TABLE_NAME = { QUOTED_IDENTIFIER }

COLUMN_CONSTRAINT = {
    ^"CHARACTER" ~ ^"SET" ~ IDENTIFIER |
    ^"COLLATE" ~ IDENTIFIER |
    ^"UNSIGNED" |
    ^"NOT" ~ ^"NULL" |
    ^"NULL" |
    ^"DEFAULT" ~ DEFAULT_VALUE |
    ^"ON" ~ ^"UPDATE" ~ ON_UPDATE_VALUE |
    ^"AUTO_INCREMENT" |
    ^"UNIQUE" |
    ^"PRIMARY" ~ ^"KEY" |
    ^"COMMENT" ~ STRING_LITERAL
}

DEFAULT_VALUE = {
    ^"NULL" |
    ^"CURRENT_TIMESTAMP" ~ ("(" ~ NUMBER ~ ")")? |
    STRING_LITERAL |
    NUMBER
}
ON_UPDATE_VALUE = {
    ^"CASCADE" |
    ^"CURRENT_TIMESTAMP" ~ ("(" ~ NUMBER ~ ")")?
}

TABLE_ELEMENT_LIST = {
    (COLUMN_DEFINITION | INDEX_DEFINITION) ~
    ("," ~ (COLUMN_DEFINITION | INDEX_DEFINITION))*
}

DATA_TYPE = {
    ((^"TINYINT" | ^"SMALLINT" | ^"MEDIUMINT" | ^"INT" | ^"INTEGER" | ^"BIGINT") ~ ("(" ~ NUMBER ~ ")")? ~ UNSIGNED? ~ ZEROFILL?) |
    ((^"DECIMAL" | ^"NUMERIC" | ^"FLOAT" | ^"DOUBLE") ~ ("(" ~ NUMBER ~ ("," ~ NUMBER)? ~ ")")? ~ UNSIGNED? ~ ZEROFILL?) |
    (^"BIT" ~ ("(" ~ NUMBER ~ ")")?) |
    ((^"DATETIME" | ^"DATE" | ^"TIMESTAMP" | ^"TIME") ~ ("(" ~ NUMBER ~ ")")?) |
    (^"YEAR" ~ ("(" ~ NUMBER ~ ")")?) |
    ((^"CHAR" | ^"VARCHAR") ~ ("(" ~ NUMBER ~ ")")? ~ CHARACTER_SET? ~ COLLATE?) |
    (^"BINARY" ~ ("(" ~ NUMBER ~ ")")?) |
    (^"VARBINARY" ~ "(" ~ NUMBER ~ ")") |
    (^"BLOB" ~ ("(" ~ NUMBER ~ ")")?) |
    (^"TINYBLOB" | ^"MEDIUMBLOB" | ^"LONGBLOB") |
    (^"TEXT" ~ ("(" ~ NUMBER ~ ")")? ~ CHARACTER_SET? ~ COLLATE?) |
    ((^"TINYTEXT" | ^"MEDIUMTEXT" | ^"LONGTEXT") ~ CHARACTER_SET? ~ COLLATE?) |
    (^"ENUM" ~ "(" ~ STRING_LITERAL ~ ("," ~ STRING_LITERAL)* ~ ")" ~ CHARACTER_SET? ~ COLLATE?) |
    (^"SET" ~ "(" ~ STRING_LITERAL ~ ("," ~ STRING_LITERAL)* ~ ")" ~ CHARACTER_SET? ~ COLLATE?) |
    ^"JSON"
}

UNSIGNED = { ^"UNSIGNED" }
ZEROFILL = { ^"ZEROFILL" }

ALTER_TABLE = {
    ^"ALTER" ~ ^"TABLE" ~ QUOTED_IDENTIFIER ~
    ALTER_SPECIFICATION ~ ("," ~ ALTER_SPECIFICATION)*
}

ALTER_SPECIFICATION = {
    ^"ADD" ~ (^"COLUMN")? ~ COLUMN_DEFINITION |
    ^"MODIFY" ~ (^"COLUMN")? ~ COLUMN_DEFINITION |
    ^"DROP" ~ (^"COLUMN")? ~ QUOTED_IDENTIFIER |
    ^"ADD" ~ INDEX_DEFINITION |
    ^"DROP" ~ ^"INDEX" ~ QUOTED_IDENTIFIER
}

DROP_TABLE = {
    ^"DROP" ~ ^"TEMPORARY"? ~ ^"TABLE" ~ IF_EXISTS? ~ QUOTED_IDENTIFIER ~ ("," ~ QUOTED_IDENTIFIER)*
}

SET_STATEMENT = {
    ^"SET" ~ VARIABLE_SET_STATMENT ~ ("," ~ VARIABLE_SET_STATMENT)*
}

VARIABLE_SET_STATMENT = {
    (IDENTIFIER | AT_MARK ~ IDENTIFIER) ~ EQUALS ~ (STRING_LITERAL | NUMBER | BOOLEAN_LITERAL | IDENTIFIER)
}

COLUMNS = {
    QUOTED_IDENTIFIER ~ ("," ~ QUOTED_IDENTIFIER)*
}

INSERT_STATEMENT = {
    ^"INSERT" ~ ^"INTO" ~ QUOTED_IDENTIFIER ~
    ("(" ~ COLUMNS ~ ")")? ~
    ^"VALUES" ~ INSERT_VALUES_LIST
}

INSERT_VALUES_LIST = {
    "(" ~ INSERT_VALUE ~ ("," ~ INSERT_VALUE)* ~ ")" ~
    ("," ~ "(" ~ INSERT_VALUE ~ ("," ~ INSERT_VALUE)* ~ ")")*
}

INSERT_VALUE = { STRING_LITERAL | NUMBER | ^"NULL" | IDENTIFIER }

UPDATE_STATEMENT = {
    ^"UPDATE" ~ QUOTED_IDENTIFIER ~
    ^"SET" ~ ASSIGNMENT_CLAUSE ~ ("," ~ ASSIGNMENT_CLAUSE)* ~
    WHERE_CLAUSE?
}

ASSIGNMENT_CLAUSE = {
    (QUOTED_IDENTIFIER | IDENTIFIER) ~ "=" ~ (STRING_LITERAL | NUMBER | ^"NULL" | IDENTIFIER)
}

DELETE_STATEMENT = {
    ^"DELETE" ~ ^"FROM" ~ QUOTED_IDENTIFIER ~
    WHERE_CLAUSE?
}

WHERE_CLAUSE = {
    ^"WHERE" ~ CONDITION ~ (AND ~ CONDITION)*
}

CONDITION = {
    (QUOTED_IDENTIFIER | IDENTIFIER) ~ COMPARISON_OPERATOR ~ (STRING_LITERAL | NUMBER | NULL | IDENTIFIER) ~
    (LOGICAL_OPERATOR ~ CONDITION)*
}

COMPARISON_OPERATOR = { EQUALS | NOT_EQUALS | LESS_THAN | GREATER_THAN | LESS_THAN_EQUALS | GREATER_THAN_EQUALS | LIKE | IN | IS_NULL | IS_NOT_NULL }
LOGICAL_OPERATOR = { AND | OR }

NUMBER = @{
    "-"? ~ (
        "0x" ~ ASCII_HEX_DIGIT+ |
        "0b" ~ ASCII_BIN_DIGIT+ |
        ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)?
    )
}

STATEMENT = _{
    CREATE_DATABASE |
    USE_DATABASE |
    CREATE_TABLE |
    ALTER_TABLE |
    DROP_TABLE |
    INSERT_STATEMENT |
    UPDATE_STATEMENT |
    DELETE_STATEMENT |
    SET_STATEMENT
}

SQL_STATEMENT = { STATEMENT ~ ";" }

MYSQL_DUMP = {
    SOI ~
    (SQL_STATEMENT | COMMENT)* ~
    EOI
}

